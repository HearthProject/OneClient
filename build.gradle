import java.security.MessageDigest

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'application'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }
}

apply from: 'https://github.com/HearthProject/OneClient/raw/master/formatter/ideconfig.gradle'

sourceCompatibility = 1.8
version = '0.0.2'

// Fetch build number from Jenkins
def ENV = System.getenv()
if (ENV.BUILD_NUMBER) {
    version = version + "." + "${System.getenv().BUILD_NUMBER}"
}

mainClassName = "com.hearthproject.oneclient.Launch"

repositories {
    mavenCentral()
	maven {
		url "https://libraries.minecraft.net/"
	}
}

configurations {
	shade
	compile.extendsFrom shade
}

dependencies {
    shade 'com.google.code.gson:gson:2.7'
	shade 'commons-io:commons-io:2.5'
	shade 'org.zeroturnaround:zt-zip:1.9'
	shade 'com.intellij:forms_rt:7.0.3'
	shade 'com.mashape.unirest:unirest-java:1.4.9'
	shade 'com.mojang:authlib:1.5.16'
	shade 'org.piwik.java.tracking:piwik-java-tracker:1.2'
	shade 'net.lingala.zip4j:zip4j:1.3.2'
	shade 'org.jsoup:jsoup:1.10.3'
	shade 'org.apache.logging.log4j:log4j-api:2.8.2'
	shade 'org.apache.logging.log4j:log4j-core:2.8.2'
	shade 'org.controlsfx:controlsfx:8.40.13'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'OneClient',
                'Implementation-Version': version,
                'Main-Class': 'com.hearthproject.oneclient.Launch'
    }
    from { configurations.shade.collect { it.isDirectory() ? it : zipTree(it) } }
}

task saveHash(dependsOn: jar) {
	doLast {
		GFileUtils.writeStringToFile(new File(jar.archivePath.getAbsolutePath() + ".sha1"), calcSha1(jar.archivePath))
	}
}

def calcSha1(file)
{
	MessageDigest md = MessageDigest.getInstance("SHA-1");
	file.eachByte 4096, {bytes, size ->
		md.update(bytes, 0, size);
	}
	return md.digest().collect {String.format "%02x", it}.join();
}

build.finalizedBy saveHash